#!/bin/bash


[[ -f fit.log ]] && rm fit.log


gnuplot << EOF


set terminal pdf enhanced

set decimalsign ","

set grid xtics ytics

set key left Left top width -3

e = 2.71828183


### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ###


set output "razem_pf.pdf"

set title "Przestrzeń fazowa"

set xlabel "położenie [m]"
set ylabel "prędkość [m/s]"

set xrange [-1.5:1.5]
set yrange [-1.5:1.5]

set format xy "%.1f"

plot    "../dane/Euler.csv" using 2:3 title "Euler" w l lt 1 lc 1,\
        "../dane/Verlet_klas.csv" using 2:3 title "Verlet klas." w l lt 1 lc 2,\
        "../dane/Verlet_pred.csv" using 2:3 title "Verlet pręd." w l lt 1 lc 3,\
        "../dane/RK_2.csv" using 2:3 title "RK 2" w l lt 1 lc 4


### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ###


set output "razem_energia.pdf"

set title "Energia"

set xlabel "czas [s]"
set ylabel "energia [J]"

set xrange [0:10]
set yrange [0.999:1.001]

set format x "%.0f"
set format y "%.4f"

set xtics 1

plot    "../dane/Verlet_klas.csv" using 1:4 title "Verlet klas." w l lt 1 lc 2,\
        "../dane/Verlet_pred.csv" using 1:4 title "Verlet pręd." w l lt 1 lc 3,\
        "../dane/RK_2.csv" using 1:4 title "RK 2" w l lt 1 lc 4


### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ###


set autoscale x

A = 1
B = 1
y(x) = B*(log(A)+x)
fit y(x) "../dane/RK_2_krok.csv" using (log(\$1)):(log(\$2)) via A,B


### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ###


set output "razem_krok.pdf"

set title "Błąd energii w zmianach kroku czasowego"

set xlabel "krok czasowy [s]"
set ylabel "pierwiastek śred. kwadratu błędu <dH^2>^{0,5}"

set logscale xy

set xrange [1e-4:1e0]
set yrange [1e-7:1e4]

set xtics 1e1

set mxtics 10

set format xy "%.0e"

plot    "../dane/Euler_krok.csv" using 1:2 title "Euler" w l lt 1 lc 1,\
        "../dane/Verlet_klas_krok.csv" using 1:2 title "Verlet klas." w l lt 1 lc 2,\
        "../dane/Verlet_pred_krok.csv" using 1:2 title "Verlet pręd." w l lt 1 lc 3,\
        "../dane/RK_2_krok.csv" using 1:2 title "RK 2" w l lt 1 lc 4
#        e**y(log(x)) w l lw 4 lc rgb "#FF00FF"


### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ###


set output "razem_krok_wzgl.pdf"

set title "Względny błąd energii w zmianach kroku czasowego"

unset logscale y
set format y "%.2f"

set yrange [0.8:1.4]

plot    "../dane/Verlet_klas_krok.csv" using 1:(\$2/(e**y(log(\$1)))) title "Verlet klas." w l lt 1 lc 2,\
        "../dane/Verlet_pred_krok.csv" using 1:(\$2/(e**y(log(\$1)))) title "Verlet pręd." w l lt 1 lc 3,\
        "../dane/RK_2_krok.csv" using 1:(\$2/(e**y(log(\$1)))) title "RK 2" w l lt 1 lc 4


### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ### ###


EOF
